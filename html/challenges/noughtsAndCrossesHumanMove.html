<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
	<head>
		<title>JOG Programming Club Challenge : Noughts And Crosses - Human Move</title>
		<meta http-equiv="Content-type" content="text/html;charset=UTF-8"> 
		<script language="javascript" src="/programmingclub/javascript/functions.js"></script>
	</head>
	<body>
		<div id="header">
			<img src="/programmingclub/img/club.png"/>
		</div> 
			<div id="navigation">
				<div class="navBar" id="mainnav_noSub">
					<a href="/programmingclub/html/index.html" class="active">Welcome</a> \
					<a href="/programmingclub/html/gallery.html">Gallery</a> \
					<a href="/programmingclub/html/challenges.html">Challenges</a> \
					<a href="/programmingclub/html/reference.html">Reference</a> \
					<a href="/programmingclub/html/contact.html">Contact</a> \
				</div>
			</div>
		</body>
		<h1>Challenge - Noughts And Crosses - Human Move</h1>

		<h3>Task</h3>
		<p><b>Draw a "nought" in the first square clicked with the mouse</b></p>
		There are 2 stages to this challenge:<p></p>
		<li>Highlight the square when the mouse is hovered over it:</li><p></p>
		<img src="/programmingclub/img/noughtsAndCrossesHumanMoveHighlight.png"/><p></p>
		<li>Draw the nought when the mouse is clicked:</li><p></p>
		<img src="/programmingclub/img/noughtsAndCrossesHumanMoveNought.png"/><p></p>

		<h3>Objective</h3>
		<p><b>Use the mouse to draw when clicked</b></p>

		<h3>Tools</h3>

		<li>The <b><a href="http://processing.org/reference/boolean">boolean</a></b> type can have only 2 possible values:
		<b><a href="http://processing.org/reference/true">true</a></b> or <b><a href="http://processing.org/reference/false">false</a></b>:</li><p></p>
		<pre><code><b>
boolean humanMove = true;
		</pre></code></b>
	
		<li>The <b><a href="http://processing.org/reference/if">if</a></b> statement is used to execute code if a condition is true:</li><p></p>
		<pre><code><b>
// if the human is moving, highlight the square currently containing the mouse
if (humanMove) {
  drawPreviewFromMousePosition();
}
		</pre></code></b>

		<li>The <b><a href="http://processing.org/reference/else">else</a></b> statement is used with <b>if</b> to execute code if a condition is <b>not</b> true:</li><p></p>
		<pre><code><b>
if (c < SCREEN_WIDTH/3) {
  return 0;
}
else if (c < 2*SCREEN_WIDTH/3) {
  return SCREEN_WIDTH/3;
}
else {
  return 2*SCREEN_WIDTH/3;
}
		</pre></code></b>

		<li>The <b><a href="http://processing.org/reference/mouseClicked_">mouseClicked</a></b> function is called by Processing when the mouse is clicked:</li><p></p>
		<pre><code><b>
void mouseClicked() {
  if (humanMove) {
    // set the nought variables to the topleft corner of the square containing the mouse
    noughtX = mouseSquareCorner(mouseX);
    noughtY = mouseSquareCorner(mouseY);
    
    // and now it will be the computer's turn next...
    humanMove = false;
  }
}
		</pre></code></b>

		<li><b>Hint:</b> The program below does most of the work.  Fill in the highlighted parts to complete the challenge:</li><p></p>
		<pre><code><b>
// noughtsAndCrossesHumanMove - allow 1 mouse move for the human (noughts)

int SCREEN_WIDTH = 600; // this game assumes a square screen!
int NOUGHT_WIDTH = 120;

// colour objects - sorry but we have to use the American spelling "color" for these!
color BACKGROUND_COLOR = color(170,200,200);
color BACKGROUND_HIGHLIGHT_COLOR = color(190,220,220);  // just a little lighter than the background
color GRID_COLOR = color(0); // black
color NOUGHT_COLOR = color(0,150,0); // dark grey

int STROKE_WEIGHT=10; // everything is drawn with a thick line for now

boolean humanMove = true; // human always moves first for now

// x,y coordinates of square containing the nought, -1 means not yet placed
int noughtX = -1;
int noughtY = -1;

void setup() {
  size(SCREEN_WIDTH, SCREEN_WIDTH);
  smooth();
}

void draw() {
  // clear everything
  background(BACKGROUND_COLOR);

  // draw the grid
  drawGrid();
  
  // draw any noughts
  drawNoughts();
  
  // if the human is moving, highlight the square currently containing the mouse
  if (humanMove) {
    drawPreviewFromMousePosition();
  }
}

// called by Processing when the mouse is clicked
void mouseClicked() {
  if (humanMove) {
    // set the nought variables to the topleft corner of the square containing the mouse
    noughtX = mouseSquareCorner(mouseX);
    noughtY = mouseSquareCorner(mouseY);
    
    // and now it will be the computer's turn next...
    humanMove = false;
  }
}

void drawNoughts() {
  // well - we can only have 0 or 1 noughts in the game, so far...
  if (noughtX != -1) {
    <b><font color="green">// we have a nought - draw it</font></b>
  }
}

void drawPreviewFromMousePosition() {
  // find the topleft corner of the square containing the mouse
  int squareX = mouseSquareCorner(mouseX);
  int squareY = mouseSquareCorner(mouseY);

  <b><font color="green">// highlight the whole square</font></b>
}

// work out the x or y coordinate of the square containing the specified coordinate
int mouseSquareCorner(int c) {
  if (c < SCREEN_WIDTH/3) {
    return 0;
  }
  else if (c < 2*SCREEN_WIDTH/3) {
    return SCREEN_WIDTH/3;
  }
  else {
    return 2*SCREEN_WIDTH/3;
  }
}

void drawGrid() {
  // draw with a thick, black line
  stroke(GRID_COLOR);
  strokeWeight(STROKE_WEIGHT);
  
  int gridWidth = SCREEN_WIDTH/3; // the width of each square
  line(0, gridWidth, SCREEN_WIDTH, gridWidth); // top horizontal line
  line(0, 2*gridWidth, SCREEN_WIDTH, 2*gridWidth); // bottom horizontal line
  line(gridWidth, 0, gridWidth, SCREEN_WIDTH); // left vertical line
  line(2*gridWidth, 0, 2*gridWidth, SCREEN_WIDTH); // right vertical line
}
		</pre></code></b>

		<h3>Example <a id="displayText" href="javascript:toggle();">(Show)</a></h1>
		<div id="toggleText" style="display: none">
		<pre><code><b>
// noughtsAndCrossesHumanMove - allow 1 mouse move for the human (noughts)

int SCREEN_WIDTH = 600; // this game assumes a square screen!
int NOUGHT_WIDTH = 120;

// colour objects - sorry but we have to use the American spelling "color" for these!
color BACKGROUND_COLOR = color(170,200,200);
color BACKGROUND_HIGHLIGHT_COLOR = color(190,220,220);  // just a little lighter than the background
color GRID_COLOR = color(0); // black
color NOUGHT_COLOR = color(0,150,0); // dark grey

int STROKE_WEIGHT=10; // everything is drawn with a thick line for now

boolean humanMove = true; // human always moves first for now

// x,y coordinates of square containing the nought, -1 means not yet placed
int noughtX = -1;
int noughtY = -1;

void setup() {
  size(SCREEN_WIDTH, SCREEN_WIDTH);
  smooth();
}

void draw() {
  // clear everything
  background(BACKGROUND_COLOR);

  // draw the grid
  drawGrid();
  
  // draw any noughts
  drawNoughts();
  
  // if the human is moving, highlight the square currently containing the mouse
  if (humanMove) {
    drawPreviewFromMousePosition();
  }
}

// called by Processing when the mouse is clicked
void mouseClicked() {
  if (humanMove) {
    // set the nought variables to the topleft corner of the square containing the mouse
    noughtX = mouseSquareCorner(mouseX);
    noughtY = mouseSquareCorner(mouseY);
    
    // and now it will be the computer's turn next...
    humanMove = false;
  }
}

void drawNoughts() {
  // well - we can only have 0 or 1 noughts in the game, so far...
  if (noughtX != -1) {
    <b><font color="green">// we have a nought - draw it
    stroke(NOUGHT_COLOR);
    strokeWeight(STROKE_WEIGHT);
    ellipse(noughtX + SCREEN_WIDTH/6, noughtY + SCREEN_WIDTH/6, NOUGHT_WIDTH, NOUGHT_WIDTH);</font></b>
  }
}

void drawPreviewFromMousePosition() {
  // find the topleft corner of the square containing the mouse
  int squareX = mouseSquareCorner(mouseX);
  int squareY = mouseSquareCorner(mouseY);

  <b><font color="green">// highlight the whole square
  // highlight the whole square
  strokeWeight(STROKE_WEIGHT);
  fill(BACKGROUND_HIGHLIGHT_COLOR);
  rect(squareX, squareY, SCREEN_WIDTH/3, SCREEN_WIDTH/3);</font></b>
}

// work out the x or y coordinate of the square containing the specified coordinate
int mouseSquareCorner(int c) {
  if (c < SCREEN_WIDTH/3) {
    return 0;
  }
  else if (c < 2*SCREEN_WIDTH/3) {
    return SCREEN_WIDTH/3;
  }
  else {
    return 2*SCREEN_WIDTH/3;
  }
}

void drawGrid() {
  // draw with a thick, black line
  stroke(GRID_COLOR);
  strokeWeight(STROKE_WEIGHT);
  
  int gridWidth = SCREEN_WIDTH/3; // the width of each square
  line(0, gridWidth, SCREEN_WIDTH, gridWidth); // top horizontal line
  line(0, 2*gridWidth, SCREEN_WIDTH, 2*gridWidth); // bottom horizontal line
  line(gridWidth, 0, gridWidth, SCREEN_WIDTH); // left vertical line
  line(2*gridWidth, 0, 2*gridWidth, SCREEN_WIDTH); // right vertical line
}
		</pre></code></b>
		</div>
	</body>
</html>
